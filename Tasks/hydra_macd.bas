''Moving Average Convergence Divergence (MACD) 
''Dataset File: EPOCH,CLOSING_PRICE,
OUTPUTPAYLOAD = ""
CURS = 0
CSZ = 64 
BUF = "" 
LIST_PRICE=LIST() 
LIST_MACD = LIST()
LIST_SIGNAL = LIST()
LIST_HISTOGRAM = LIST()


DEF DO_EMA(LISTA, PERIOD, WHERE)
    MULTIPLIER = 2.0 / (PERIOD + 1.0)
    EMA = 0
    FIRST_EMA = 1
    LLEN = LEN(LISTA) -1
    FOR I = 0 TO LLEN
        PRICE = GET(LISTA, I)
        IF FIRST_EMA = 1 AND I >= PERIOD - 1 THEN
            SUM = 0
            FOR J = I - PERIOD + 1 TO I
                SUM = SUM + GET(LISTA, J)
            NEXT J
            EMA = SUM / PERIOD
            FIRST_EMA = 0
        ELSEIF FIRST_EMA = 0 THEN
            EMA = (PRICE * MULTIPLIER) + (EMA * (1 - MULTIPLIER))
        ENDIF
        PUSH(WHERE, EMA)
    NEXT I
ENDDEF
DEF DO_MACD(LISTA)
    EMA_SHORT_PERIOD = 12
    EMA_LONG_PERIOD = 26
    SIGNAL_PERIOD = 9
    LIST_EMA_SHORT = LIST()
    LIST_EMA_LONG = LIST()

    DO_EMA(LISTA, EMA_SHORT_PERIOD,LIST_EMA_SHORT)
    DO_EMA(LISTA, EMA_LONG_PERIOD,LIST_EMA_LONG)

    FOR I = 0 TO LEN(LISTA) - 1
        MACD_VALUE = GET(LIST_EMA_SHORT, I) - GET(LIST_EMA_LONG, I)
        PUSH(LIST_MACD, MACD_VALUE)
    NEXT I

    ''  Calculate Signal Line
    DO_EMA(LIST_MACD, SIGNAL_PERIOD,LIST_SIGNAL)

    '' Optionally, calculate MACD Histogram (MACD Line - Signal Line)
    FOR I = 0 TO LEN(LIST_MACD) - 1
        HISTOGRAM_VALUE = GET(LIST_MACD, I) - GET(LIST_SIGNAL, I)
        PUSH(LIST_HISTOGRAM, HISTOGRAM_VALUE)
    NEXT I

ENDDEF

DEF TOK(WHR,TSP)
    CLEAR(WHR)
	CRS=""
	SLEN=LEN(TSP)-1
	FOR ICT=0 TO SLEN
	CHARV=MID(TSP,ICT,1)
	IF CHARV<>"," THEN
	CRS=CRS+CHARV
	ELSE
	PUSH(WHR,CRS)
	CRS=""
	ENDIF
	NEXT ICT
ENDDEF 
DEF FNL(STRS)
    SLEN = LEN(STRS) - 1
    FOR ICT = 0 TO SLEN
        CCC = MID(STRS, ICT, 1)
        IF ASC(CCC) = 10 THEN RETURN ICT
    NEXT ICT
    RETURN -1
ENDDEF 
DEF RLN(FLEN)
    NLN = ""
    NBF = ""
    IF CURS+CSZ > FLEN THEN CSZ = FLEN - CURS
    IF LEN(BUF) < CSZ THEN
        NBF = READ(CURS, CSZ)
        CURS = CURS + LEN(NBF)  
    ENDIF
    IF NBF <> "" THEN BUF = BUF + NBF
    POS = FNL(BUF)
    IF POS <> -1 THEN
        NLN = LEFT(BUF, POS)
        BUF = RIGHT(BUF, LEN(BUF) - POS - 1)
    ELSEIF CURS >= FLEN THEN
        NLN = BUF
        BUF = ""
    ENDIF
    RETURN NLN
ENDDEF 

PAYLOAD=LIST() 
TOK(PAYLOAD,INPUTPAYLOAD)
FNAM = GET(PAYLOAD,0)
PERIOD = VAL(GET(PAYLOAD,1))

GETIT = DOWNLOAD(FNAM)
IF GETIT <= 0 THEN EXIT 

LAL=LIST()
WHILE TRUE
    LINE = RLN(GETIT)
    IF LINE = "" OR CURS >= GETIT THEN EXIT	
	TOK(LAL,LINE)
	EPOCH=GET(LAL,0)	
	VVV=GET(LAL,1)
	PUSH(LIST_PRICE,VAL(VVV))	
    PRINT ">", EPOCH;
WEND

DO_MACD(LIST_PRICE)
CLEAR(LIST_PRICE)

FOR I = 0 TO LEN(LIST_MACD) - 1
X = GET(LIST_MACD,I)
Y = GET(LIST_SIGNAL,I)
Z = GET(LIST_HISTOGRAM,I)
OUTPUTPAYLOAD=OUTPUTPAYLOAD+STR(X)+","+STR(Y)+","+STR(Z)+CHR(10)
NEXT I